########## NAME
alcohol_model_28

########## METADATA
time_unit = m

########## MACROS

########## STATES
// gastric emptying
d/dt(Vol_Stomach) = + vol_drink_per_time - r_Vol_emptying // L

// Liquid kcal
d/dt(Kcal_Liquid) = + vol_drink_per_time*kcal_liquid_per_vol                                                            // kcal [ L * kcal/L = kcal]
d/dt(Kcal_remain) = + vol_drink_per_time*kcal_liquid_per_vol - r_Vol_emptying*stomach_kcal_liquid_per_vol               // kcal [ L * kcal/L = kcal]

// solid kcal
d/dt(max_Kcal_Solid) = 0
d/dt(Kcal_Solid) = - r_digestion

// Gastric conc
d/dt(EtOH_Pool) = + r_poolIn*Vol_Stomach - r_poolOut                    //mg
d/dt(Stomach_Conc) = r_drinkEtOH - r_poolIn + r_poolOut/Vol_Stomach - r_stomachADH // mg/dL

// intestines
d/dt(Mass_EtOH_Intestines) = + r_Vol_emptying*Stomach_Conc - r_EtOH_uptake          // mg

// Blood and metabolites
d/dt(Blood_Conc_Central)  =  + (r_EtOH_uptake/V_Blood_Central) - r_circulation         //mg/dL 
d/dt(Blood_Conc_Peripheral)  =  + r_circulation*(V_Blood_Central/V_Blood_Peripheral) - r_Tissue_Peripheral - r_Liver + (r_urine_return - r_urine_minimal_mass - r_urine)/V_Blood_Peripheral    //mg/dL 
d/dt(Tissue_Conc) =  r_Tissue_Peripheral*(V_Blood_Peripheral/V_Tissue)
d/dt(Liver_Conc)  =  r_Liver*(V_Blood_Peripheral/V_Liver) - r_EtOH_Elimination - r_EtG - r_EtS             //mg/dL 

d/dt(Plasma_acetate) = + r_EtOH_Elimination - r_acetate                  //mg/dL  

d/dt(Tissue_extra_water) = r_Vol_emptying*10 - r_Tissue_urine   //dL
d/dt(Vasopressin) = r_vasopressin_influx - r_vasopressin_clearance

d/dt(Urine_Mass_EtOH) = r_urine_minimal_mass + r_urine - r_urine_return         // mg
d/dt(Urine_Volume) = r_urine_minimal_vol + r_Tissue_urine     // dL
d/dt(EtG) = r_EtG - r_EtG2
d/dt(EtG2) = r_EtG2*(V_Liver/V_Blood_Peripheral) - r_EtG_urine
d/dt(Urine_Mass_EtG) = r_EtG_urine*V_Blood_Peripheral
d/dt(EtS) = r_EtS - r_EtS2
d/dt(EtS2) = r_EtS2*(V_Liver/V_Blood_Peripheral) - r_EtS_urine
d/dt(Urine_Mass_EtS) = r_EtS_urine*V_Blood_Peripheral

d/dt(PEth) = r_PEth - r_PEth_bound + r_PEth_release - r_PEth_clearance 
d/dt(PEth_Bound) =  r_PEth_bound - r_PEth_release

d/dt(time_elapsed)=1


Vol_Stomach(0)           = SS_vol
Kcal_Liquid(0)           = 0
Kcal_remain(0)           = 0
max_Kcal_Solid(0)        = 0
Kcal_Solid(0)            = 0
EtOH_Pool(0)             = 0
Stomach_Conc(0)          = 0
Mass_EtOH_Intestines(0)  = 0
Blood_Conc_Central(0)    = 0
Blood_Conc_Peripheral(0) = 0
Tissue_Conc(0)           = 0
Liver_Conc(0)            = 0
Plasma_acetate(0)        = 0
Tissue_extra_water(0)    = 0
Vasopressin(0)           = k_basal_vasopressin/k_clearance_vasopressin
Urine_Mass_EtOH(0)       = 0
Urine_Volume(0)          = SS_urine_vol
EtG(0)                   = 0
EtG2(0)                  = 0
EtS(0)                   = 0
EtS2(0)                  = 0
Urine_Mass_EtG(0)        = 0
Urine_Mass_EtS(0)        = 0
PEth(0)                  = 0
PEth_Bound(0)            = 0
time_elapsed(0)          = 0

########## PARAMETERS
VmaxGastric             = 0.1019548659749525
KmGastric               = 0.8414274811576545
k_kcalscaling           = 0.027008647776467148
km_kcal                 = 7011.360478051468
n_kcal                  = 2.857824329109693
k_poolIn                = 0.04511475855764087
k_poolOut               = 1.2432975002263036e-07
VmaxADHSto              = 281.5900511040591
KmADHSto                = 1816.2285602464694
k_EtOHuptake            = 0.05063450856720283
k_equalize              = 2.406129562745927
k_equalize_liver        = 0.5335879826950733
VmaxADH1                = 7.518202774494303
VmaxCYP2E1              = 0.5440344149284243
KmADH1                  = 1.7748693810830143
KmCYP2E1                = 45.78776587265418
k_food_clearance        = 0.13242844280032146
k_acetate               = 1.1933039370283853
k_blood_plasma_ratio    = 0.6963930437339009
k_urine_water           = 6.018578839280425
k_basal_vasopressin     = 9.141975175677507
k_vasopressin_BAC       = 0.4189221547283918
k_clearance_vasopressin = 0.006260176958389283
k_kidney_permability    = 3.104347175039371e-05
VmaxEtG                 = 0.29139597710825776
KmEtG                   = 49905.697330564224
VmaxEtS                 = 0.0026025413905416185
KmEtS                   = 325.2614447905694
k_EtG_delay             = 0.0016620859498443078
k_EtS_delay             = 0.01055440638097244
k_EtG_urine             = 0.0015491346385385896
k_EtS_urine             = 44971.12910748395
k_PEth                  = 0.1734614145531799
k_PEth_clearance        = 0.002025850055545138
k_PEth_bind             = 0.10649655255360564
k_PEth_release          = 0.002736160117302702

########## VARIABLES
SS_vol = 0.001
SS_urine_vol = 0.05 // Post Void Residual Volume https://www.ncbi.nlm.nih.gov/books/NBK539839/

V_Liver = (18.51 * weight + 191.8)/100  // [ml/100]=dL Vauthey formula 10.1053/jlts.2002.31654
V_Blood = ((1-sex)*(0.3561 * height^3 + 0.03308 * weight + 0.1833) + sex*(0.3669 * height^3 + 0.03219 * weight + 0.6041))*10 // dL     Nadler's Equation for total blood volume
V_Blood_Peripheral = 0.85*V_Blood
V_Blood_Central = 0.15*V_Blood
V_Plasma_Peripheral = k_blood_plasma_ratio*V_Blood_Peripheral    // k_blood_plasma_ratio is the factor of "accessible" water for EtOH - hemocrit values https://www.ncbi.nlm.nih.gov/books/NBK259/

TBW = ( (1-sex)*((-2.097+0.1069*(height*100)+0.2466*weight)/(0.84*weight)) + sex*((2.447-0.09516*age+0.1074*(height*100)+0.3362*weight)/(0.84*weight)) )*weight*10 // rho factor [L/kg] source Watson et.al 10.15288/jsa.1981.42.547, found in Meskel et.al. 10.1016/j.forsciint.2018.10.033 
V_Tissue = TBW - V_Blood - V_Liver + Tissue_extra_water

conc_drink = (EtOH_conc*789.1*10) // mg/L

kcal_solid_vol = max(1,Kcal_Solid)/(4*1000) // 4kcal/g assumes 1g/ml, 1000 scales ml--> LL 

vADH1 = VmaxADH1*(Liver_Conc/( KmADH1 + Liver_Conc)) 
vCYP2E1 = (VmaxCYP2E1*Liver_Conc/( KmCYP2E1 + Liver_Conc))  

stomach_kcal_liquid_per_vol     = Kcal_remain/Vol_Stomach

// Model reactions
kcal_effect = (1 - (Kcal_Liquid/k_kcalscaling)^n_kcal/( (Kcal_Liquid/k_kcalscaling)^n_kcal + km_kcal^n_kcal) )
r_Vol_emptying = VmaxGastric*( (Vol_Stomach-SS_vol)/((Vol_Stomach-SS_vol) + KmGastric) ) * kcal_effect
r_digestion = (max_Kcal_Solid * 1.88 * 0.010* (0.010*max(0, time_elapsed))^0.86  * exp(-(0.010*max(0, time_elapsed))^1.86))    // doi: 10.1016/S0002-9270(00)00868-6

r_poolIn  = gt(Vol_Stomach, 2*SS_vol) * max(0, (Stomach_Conc - EtOH_Pool/kcal_solid_vol) * k_poolIn)
r_poolOut =  gt(Vol_Stomach, 2*SS_vol) * EtOH_Pool * k_poolOut

r_drinkEtOH = (conc_drink - Stomach_Conc)*(vol_drink_per_time/Vol_Stomach)
r_stomachADH = VmaxADHSto*(Stomach_Conc / (Stomach_Conc + KmADHSto))

r_EtOH_uptake = Mass_EtOH_Intestines*k_EtOHuptake 

r_EtOH_Elimination  = (vADH1 + vCYP2E1)*( 1 + k_food_clearance*gt(Kcal_Solid, 1e-3))
r_Liver = k_equalize_liver*(Blood_Conc_Peripheral-Liver_Conc)
r_Tissue_Peripheral = (Blood_Conc_Peripheral-Tissue_Conc)
r_circulation = k_equalize*(Blood_Conc_Central-Blood_Conc_Peripheral)
r_acetate = Plasma_acetate*k_acetate

r_urine_minimal_vol  = 0.01 // [dL] 1mL is minimal urine production per min
r_urine_minimal_mass = r_urine_minimal_vol*Blood_Conc_Peripheral // [mg] 
r_Tissue_urine       = (Tissue_extra_water/Vasopressin)*k_urine_water // [dL]

r_urine = r_Tissue_urine*( (Blood_Conc_Peripheral*V_Blood_Peripheral)/V_Plasma_Peripheral )  // [mg] rescale from BAC to plasma conc
r_urine_return = k_kidney_permability * Vasopressin * ( (Urine_Mass_EtOH/Urine_Volume) - ((Blood_Conc_Peripheral*V_Blood_Peripheral)/V_Plasma_Peripheral) ) * Urine_Volume  // [mg] plasma-urine conc difference - times volume for mass - less permeable with lower Vasopressin

r_vasopressin_influx = k_basal_vasopressin/(1 + k_vasopressin_BAC*Blood_Conc_Peripheral)
r_vasopressin_clearance = Vasopressin*k_clearance_vasopressin

r_EtG  = VmaxEtG*(Liver_Conc/(Liver_Conc + KmEtG)) 
r_EtG2 = EtG*k_EtG_delay
r_EtS  = VmaxEtS*(Liver_Conc/(Liver_Conc + KmEtS)) 
r_EtS2 = EtS*k_EtS_delay

r_EtG_urine = k_EtG_urine*EtG2
r_EtS_urine = k_EtS_urine*EtS2

r_PEth = k_PEth*Blood_Conc_Peripheral
r_PEth_clearance = k_PEth_clearance*PEth
r_PEth_bound = k_PEth_bind*PEth
r_PEth_release = max(0, k_PEth_release*( PEth_Bound - PEth ))

########## FUNCTIONS

########## EVENTS
new_drink = gt(vol_drink_per_time, 0), Kcal_Liquid, Kcal_remain
new_meal = gt(meal_kcal, 0), max_Kcal_Solid, meal_kcal+Kcal_Solid, Kcal_Solid, meal_kcal+Kcal_Solid, time_elapsed, 0 
food_digested = le(Kcal_Solid, 1e-3), Kcal_Solid, 0, max_Kcal_Solid, 0, EtOH_Pool, 0
urinate = gt(urinate, 0), Urine_Volume, SS_urine_vol, Urine_Mass_EtOH, SS_urine_vol*(Urine_Mass_EtOH/Urine_Volume), Urine_Mass_EtG, SS_urine_vol*(Urine_Mass_EtG/Urine_Volume), Urine_Mass_EtS, SS_urine_vol*(Urine_Mass_EtS/Urine_Volume)

########## OUTPUTS

########## INPUTS
EtOH_conc           = EtOH_conc                     @ 0
vol_drink_per_time  = vol_drink_per_time            @ 0
kcal_liquid_per_vol = kcal_liquid_per_vol           @ 0
meal_kcal           = kcal_solid                    @ 0

sex                 = sex                           @ 1       // male=1, female=0
weight              = weight                        @ 104     // Kg
height              = height                        @ 1.85    // m
age                 = age                           @ 25      // years

urinate             = urinate                       @ 0

########## FEATURES
Blood alcohol concentration (mg/dL) = Blood_Conc_Peripheral   // mg/dL
Blood alcohol concentration (‰) = Blood_Conc_Peripheral/100 
Acetate in plasma (mM) = Plasma_acetate/10.2                              //mg/dL --> mM
Breath alcohol concentration (g/210L) = 0.840*(Blood_Conc_Central/1000) + 0.00367                // BrAC [g/210L = 0.840*BAC[g/dL]+0.00367 => 1e-3*g/dL]
Breath alcohol concentration (g/L) = ( 0.840*(Blood_Conc_Central/1000) + 0.00367 )    // g/dL BAC equals g/210L BrAC 
Gastric volume (mL) = Vol_Stomach*1000   // L->mL
PEth (ng/mL) = PEth
Urine alcohol concentration (mg/dL) = (Urine_Mass_EtOH/Urine_Volume)    // mg/dL
Urine alcohol concentration (‰) = (Urine_Mass_EtOH/Urine_Volume)/100
Ethyl glucuronide; EtG (mg/dL) = EtG
Ethyl sulfate; EtS (mg/dL) = EtS